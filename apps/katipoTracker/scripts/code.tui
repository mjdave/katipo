

knownHosts = {}
knownClients = {}

KATIPO_VERSION = "0.1" #todo

### loop over the launch arguments

argCount = table.count(launchArgs)
for(i = 0, i < argCount, i++)
{
    arg = launchArgs[i]

    if(arg == "--hostPort")
    {
        if(i+1 >= argCount)
        {
            print("missing argument. usage example: ./katipoTracker --hostPort 3470")
            return
        }
        i++
        katipo.hostPort = launchArgs[i]
    }
    else if(arg == "--clientPort")
    {
        if(i+1 >= argCount)
        {
            print("missing argument. usage example: ./katipoTracker --clientPort 3471")
            return
        }
        i++
        katipo.clientPort = launchArgs[i]
    }
    else if(arg == "--help")
    {
        print("")
        print("Katipo Tracker version:", KATIPO_VERSION)
        print("Example usage: ./katipoTracker --hostPort 3470 --clientPort 3471")
        print("")
        print("--basePath PATH") #this is handled by Controller.cpp
        print("  Were to find scripts/code.tui")
        print("  optional, default: the same directory as the katipoTracker binary")
        print("")
        print("--hostPort PORT")
        print("  The port hosts will connect to.")
        print("  optional, default: 3470")
        print("")
        print("--clientPort PORT")
        print("  The port clients will connect to.")
        print("  optional, default: 3471")
        return
    }
}

### initialize katipo now that we have loaded the launch args
success = katipo.init()

if(!success)
{
    print("Failed to initialize katipo.")
    return
}

hostServer = katipo.hostServer
clientServer = katipo.clientServer

### hosts

hostServer.clientConnected = function(clientID, clientInfo)
{
    print("host connected:", clientID, " clientInfo:", clientInfo)
    knownClients[clientID] = clientInfo
}


hostServer.clientDisconnected = function(clientID) #todo
{
    knownClients[clientID] = nil
}

hostSetHostinfo = function(clientID, hostInfo)
{
    print("got hostInfo:", hostInfo)

    hostNameKey = hostInfo.hostNameKey
    if(!hostNameKey)
    {
        message = "missing host key"
        print("Error:", message)
        return {
            status = "error"
            message = message
        }
    }

    loadedHostInfo = knownHosts[hostNameKey]

    if(loadedHostInfo)
    {
        if(clientID != loadedHostInfo.clientID)
        {
            message = "host name already in use by another host"
            print("Error:", message)
            return {
                status = "error"
                message = message
            }
        }
    }
    else
    {
        loadedHostInfo = {
            clientID = clientID
        }
        knownHosts[hostNameKey] = loadedHostInfo
    }

    loadedHostInfo.info = hostInfo

    print("knownHosts:", knownHosts)
    
    return {
        status = "ok"
    }
}

hostServer.register("setHostinfo", hostSetHostinfo)

### clients

clientServer.clientConnected = function(clientID)
{
    print("client connected:", clientID, " initialData:", initialData)
}

callHostFunction = function(clientID, url, clientData, callbackFunc)
{
    print("client callHostFunction:", clientID, " url:", url, " clientData:", clientData)

    if(url)
    {
        hostName = url #todo support host/subFunc
        hostInfo = knownHosts[url]
        if(hostInfo)
        {
            print("calling host server func")
            hostServer.callClientFunction(hostInfo.clientID, "get", url, clientData, callbackFunc)
        }
        else
        {
            result = {
                status = "error"
                message = "unknown host:" + url
            }
            callbackFunc(result)

            //TODO causes tui crash:
            //callbackFunc({
            //    status = "error"
            //    message = "unknown host:" + url
            //})
        }
    }
    else
    {
        callbackFunc({
            status = "error"
            message = "missing host url"
        })
    }
}

#called by client
clientServer.register("callHostFunction", callHostFunction)



### start the servers
success = katipo.start()

if(!success)
{
    print("Failed to start katipo servers.")
    return
}

print("katipoTracker is running with host port ", katipo.hostPort, " and client port ", katipo.clientPort, " and is ready for connections.
Type 'help' for commands."
)

while(true)
{
    value = readValue()

    if(value == "help")
    {
        print("commands:
\tquit|exit|q exits")
    }
    else if(value == "list")
    {
        //printConnectedLists()
    }
    else if(value == "quit" or value == "exit" or value == "q")
    {
        break
    }
}